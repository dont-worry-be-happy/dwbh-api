
/**
 * Configuration to use JUnit 5
 *
 * @since 0.1.0
 */
test {
    useJUnitPlatform()
}

/**
 * Configuring PMD static analyzer
 *
 * @since 0.1.0
 */
pmd {
    toolVersion "6.11.0"
    ignoreFailures = false
    ruleSetFiles 'etc/pmd/rulesets-general.xml'
}

/**
 * Configuring syntax checker/formatter
 *
 * @since 0.1.0
 */
spotless {
    java {
        // licenseHeaderFile 'etc/spotless/apache.license.header.txt'
        importOrder 'spotless.importorder'
        googleJavaFormat()
        removeUnusedImports()
    }
}

/**
 * Configuring latest version of bugs finder
 *
 * @since 0.1.0
 */
spotbugs {
    toolVersion = '3.1.11'
}

/**
 * Enabling HTML reports for bugs finder (main)
 *
 * @since 0.1.0
 */
spotbugsMain {
    reports.xml.enabled = false
    reports.html.enabled = true
}

/**
 * Enabling HTML reports for bugs finder (test)
 *
 * @since 0.1.0
 */
spotbugsTest {
    reports.xml.enabled = false
    reports.html.enabled = true
}

/**
 * Using latest jacoco version
 *
 * @since 0.1.0
 */
jacoco {
    toolVersion = "0.8.2"
}

/**
 * Getting rid of some generated classes
 *
 * @since 0.1.0
 */
jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/*$*Definition*/**')
        })
    }
}

/**
 * Making sure Jacoco is executed at the end of
 * the build task
 *
 * @since 0.1.0
 */
build.finalizedBy(jacocoTestReport)